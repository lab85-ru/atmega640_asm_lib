;==================================================================================
; управление вентилятором
;
;==================================================================================

; константы температур включения выключения Вентилятора
.equ TEMP_FAN_ON = 35
.equ TEMP_FAN_OFF = 30


;==================================================================================
; fan on
;==================================================================================
fan_on:

			lds			r16,fan_on_port
			ori			r16,(1 << fan_on_pin)
			sts			fan_on_port,r16
ret

;==================================================================================
; fan off
;==================================================================================
fan_off:

			lds			r16,fan_on_port
			andi		r16,~(1 << fan_on_pin)
			sts			fan_on_port,r16
ret

;==================================================================================
; fan управление с гистерезисом ВКЛ при Т>=35, Выкл при Т<=30
;==================================================================================
fan_control:

				ldi		xl,low(ow_read_buf)		; буфер, пакет от датчика температуры
				ldi		xh,high(ow_read_buf)

				ld		r16,x+					; low
				ld		r17,x					; high

				cpi		r17,0xff
				breq	_fan_control_minus		; отрицательная температура

				clc
				ror		r16						; /2 тк самый младший бит .5 градуса по цельсию

				jmp		_fan_control_plus

_fan_control_minus:
				com		r16						; переводим число из отрицательного в положительное
				inc		r16

				clc
				ror		r16						; /2 тк самый младший бит .5 градуса по цельсию

				cpi		r16,0x00
				breq	_fan_control_plus			; нулевая температура

				call	fan_off					; t<0 fan OFF
				ret

_fan_control_plus:
				cpi 	r16,TEMP_FAN_ON			; >=
				brsh	_fan_control_on

				cpi 	r16,TEMP_FAN_OFF		; <
				brlo	_fan_control_off

				ret

_fan_control_on:
				call	fan_on  
				ret


_fan_control_off:
				call	fan_off
				ret

